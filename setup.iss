; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

;#include ReadReg(HKLM, 'Software\WOW6432Node\Mitrich Software\Inno Download Plugin', 'InstallDir') + '\idp.iss'
;#define DwinsHs_Data_Buffer_Length 655536

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{31E816D6-833D-4FA9-8629-D031F10AC55E}
AppName=SalvageCalculator
AppVersion=2.0.0
;AppVerName=SalvageCalculator 1.0
AppPublisher=Kur0
AppPublisherURL=https://github.com/jericjan
AppSupportURL=https://github.com/jericjan
AppUpdatesURL=https://github.com/jericjan
DefaultDirName={autopf}\SalvageCalculator
DisableProgramGroupPage=yes
; Uncomment the following line to run in non administrative install mode (install for current user only.)
;PrivilegesRequired=lowest
PrivilegesRequiredOverridesAllowed=commandline
OutputBaseFilename=SalvageCalculatorSetup
Compression=lzma
SolidCompression=yes
WizardStyle=modern

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "C:\Users\USER\Desktop\JJ\python\honkai-scanner\dist\SalvageCalculator\SalvageCalculator.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Users\USER\Desktop\JJ\python\honkai-scanner\dist\SalvageCalculator\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
; These files will be downloaded
Source: "{tmp}\tesseract-setup.exe"; DestDir: "{app}"; Flags: deleteafterinstall external skipifsourcedoesntexist
; Source: "tesseract-setup.exe"; DestDir: "{tmp}"; Flags: deleteafterinstall external; ExternalSize: 56238080; Check: NotInstalled
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Code]
function GetHKLM: Integer;
begin
  if IsWin64 then
    Result := HKLM64
  else
    Result := HKLM32;
end;
function NotInstalled: Boolean;
begin
  Result :=
    not RegKeyExists(
      GetHKLM, 'SOFTWARE\Tesseract-OCR');
end;

var
  DownloadPage: TDownloadWizardPage;

var
  TesseractURL: string;


function OnDownloadProgress(const Url, FileName: String; const Progress, ProgressMax: Int64): Boolean;
begin
  if Progress = ProgressMax then
    Log(Format('Successfully downloaded file to {tmp}: %s', [FileName]));
  Result := True;
end;
procedure InitializeWizard;
begin

  if NotInstalled then
    begin
    // MsgBox(Format('NotInstalled = %d', [NotInstalled]), mbInformation, MB_OK);
    if IsWin64 then
      begin
        TesseractURL:= 'https://digi.bib.uni-mannheim.de/tesseract/tesseract-ocr-w64-setup-v5.2.0.20220712.exe';
      end
    else
      TesseractURL:= 'https://digi.bib.uni-mannheim.de/tesseract/tesseract-ocr-w32-setup-v5.2.0.20220712.exe';
    end;
  DownloadPage := CreateDownloadPage(SetupMessage(msgWizardPreparing), SetupMessage(msgPreparingDesc)+ #13#10 + 'It will freeze and I have no idea why but you just have to deal with it for now.', @OnDownloadProgress);
end;
function NextButtonClick(CurPageID: Integer): Boolean;
begin
  if NotInstalled then
    begin
      if CurPageID = wpReady then begin
        DownloadPage.Clear;        
        DownloadPage.Add(TesseractURL, 'tesseract-setup.exe', '');
        DownloadPage.Show;
        try
          try
            DownloadPage.Download; // This downloads the files to {tmp}
            Result := True;
          except
            if DownloadPage.AbortedByUser then
              Log('Aborted by user.')
            else
              SuppressibleMsgBox(AddPeriod(GetExceptionMessage), mbCriticalError, MB_OK, IDOK);
            Result := False;
          end;
        finally
          DownloadPage.Hide;
        end;
      end else
        Result := True;
    end
  else
    Result := True;
end;


[Icons]
Name: "{autoprograms}\SalvageCalculator"; Filename: "{app}\SalvageCalculator.exe"
Name: "{autodesktop}\SalvageCalculator"; Filename: "{app}\SalvageCalculator.exe"; Tasks: desktopicon

[Run]
Filename: "{app}\SalvageCalculator.exe"; Description: "{cm:LaunchProgram,SalvageCalculator}"; Flags: nowait postinstall skipifsilent
Filename: "{tmp}\tesseract-setup.exe"; Check: NotInstalled
